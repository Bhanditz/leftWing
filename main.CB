DefaultMask OFF
'FrameLimit 64

Include "customfunctions.cb"

Const SW = 800
Const SH = 600
Global SS : SS = Distance(0,0,SW,SH)

Global GROUND_ROCK : GROUND_ROCK     = 0
Global GROUND_DIRT : GROUND_DIRT     = 1
Global GROUND_BUILDING : GROUND_BUILDING = 2

Global dbx : dbx = 10
Global skyMasked, sky, BGsky
Global player

Global sx : sx = SW+100
Global sy : sy = -100

Dim Ground(SW, 5)
'(i,0)			Korkeus
'	(i,1) = 0	KIVI
'	(i,1) = 1	MAA
'		(i,3)		mullan syvyys
'	(i,1) = 2	TALO
'		(i,2)		Talon alkukohta
'		(i,3)		mullan syvyys
'		(i,4)		hit-offset-korkeus


Type CloudBlock
	Field x As Float
	Field y As Float
	Field xp As Float
	Field yp As Float
	Field radius As Float
	Field r
	Field g
	Field b
	Field a
End Type

Type Cloud
	Field pos 'muistipala
	Field blocksCount 'Pilvenpalojen m‰‰r‰
	Field blocks 'mustipala jossa pilven palat
End Type

Type Clr
	Field r As Float
	Field g As Float
	Field b As Float
	Field a As Float
End Type

Type Vector2
	Field x As Float
	Field y As Float
End Type

Type Vector3
	Field x As Float
	Field y As Float
	Field z As Float
End Type

Type Plane
	Field x					' x
	Field y					' y
	Field xp As Float
	Field yp As Float
	Field angle				' kiihtyvyys
	Field acc
	Field speed				' nopeus
	Field length			' pituus (vaikutanee k‰‰ntyvyyteen)

	Field mhealth			' terveys
	Field health			' terveys
	Field lifes				' el‰m‰t

	Field points As Float	' pisteet
End Type

Type Bomb
	Field x
	Field y
	Field xp As Float
	Field yp As Float
	Field m As Float
End Type

Type Particle
	Field x
	Field y
	Field speed
	Field a
	Field xp As Float
	Field yp As Float
	Field m As Float
	Field r
	Field g
	Field b

	Field ttime
	Field tstart
	Field tr
	Field tg
	Field tb

	Field atime
	Field astart
End Type

Type Explode
	Field x
	Field y
	Field radius As Float
	Field mradius As Float

	Field r As Float
	Field g As Float
	Field b As Float
	Field alpha As Float

	Field ttime
	Field tstart
	Field tr As Float
	Field tg As Float
	Field tb As Float

	Field atime
	Field astart
End Type

Type Enemy
	Field x
	Field y
	Field yp As Float
	Field xp As Float
	Field angle As Float

	Field mhealth			' terveys
	Field health
	Field sizeW
	Field sizeH
End Type

' Alustetaan lentokoneen tyyppi
tPlane.Plane	= New(Plane)
tPlane\health	= 100
tPlane\lifes	= 3
tPlane\speed	= 3
tPlane\length	= 5

tPlane\x		= 400
tPlane\y		= 200
tPlane\angle	= GetAngle(tPlane\x, tPlane\y, MouseX(), MouseY())

player = ConvertToInteger(tPlane)
skyMasked = MakeImage(SW+10, SH+10)
BGsky = MakeImage(SW+10, SH+10)
cbeSetLineWidth(15)
DrawToImage BGsky
	Color 0,0,255
	For x = -10 To SW Step 15
		cbeColor(Rand(50,100), Rand(50,100), Rand(0,0), 150)
		Line sx, sy, x, SH+10
	Next x
	For y = 0 To SH+10 Step 15
		cbeColor(Rand(50,100), Rand(50,100), Rand(0,0), 150)
		Line sx, sy, -10, y
	Next y
DrawToScreen

sky = MakeImage(SW+10, SH+10)
DrawToImage sky
	Color 0,0,255
	Box 0,0,SW,SH
	For x = -10 To SW Step 15
		cbeColor(Rand(200,255), Rand(200,255), Rand(0,50), 150)
		Line sx, sy, x, SH+10
	Next x
	For y = 0 To SH+10 Step 15
		cbeColor(Rand(200,255), Rand(200,255), Rand(0,50), 150)
		Line sx, sy, -10, y
	Next y
DrawToScreen
cbeSetLineWidth(1)

SCREEN SW,SH

GenerateClouds()
GenerateGround()

Repeat

	UpdateBG()
	Controls()
	UpdateClouds()
	UpdateBG()
	UpdateEntitets()
	DrawBG()
	DrawGround()
	DrawEntitets()
	DrawClouds()
	DrawGUI()
	
	SetWindow FPS()+""
	DrawScreen

	ClsColor 0,0,0
Until EscapeKey()



Function Controls()
	tPlane.Plane = ConvertToType(player)

	na = GetAngle(tPlane\x, tPlane\y, MouseX(), MouseY())
	tPlane\angle = CurveAngle(na, tPlane\angle, 12)

	If MouseHit(1) Then
		b.Bomb = New(Bomb)
		b\x = tPlane\x
		b\y = tPlane\y
		b\xp = tPlane\xp
		b\yp = tPlane\yp
		b\m = 0.1
	EndIf

	dbx = dbx - KeyHit(cbKeyLeft) + KeyHit(cbKeyRight)
	
	
	If (KeyHit(64)) Then GenerateClouds()
	If (KeyHit(63)) Then GenerateGround()
End Function 

Function UpdateBG()
	DrawToImage skyMasked
		Box 0, 0, SW, SH
		DrawImage sky, 0, 0
		cbeSetBlendMode(CBE_BLEND_OVERWRITE, 1)
		cbeColor(255,0,255,0)
		For c.CloudBlock = Each CloudBlock
			alpha# = GetAngle(sx, sy, c\x, c\y)
	
			s# = Distance(sx, sy, c\x, c\y)
			t# = Distance(sx, sy, c\x, c\y)
	
			tx0# = c\x + c\radius * Cos(alpha+90)
			ty0# = c\y - c\radius * Sin(alpha+90)
			dist0# = Distance(sx, sy, tx0, ty0)
			gamma0# = GetAngle(sx, sy, tx0, ty0)
	
			tx1# = c\x + c\radius * Cos(alpha-90)
			ty1# = c\y - c\radius * Sin(alpha-90)
			dist1# = Distance(sx, sy, tx1, ty1)
			gamma1# = GetAngle(sx, sy, tx1, ty1)
	
			cbeTriangle(sx + dist0 * Cos(gamma0), sy - dist0 * Sin(gamma0), sx + 2 * SS * Cos(gamma0), sy - 2 * SS * Sin(gamma0), sx + 2 * SS * Cos(gamma1), sy - 2 * SS * Sin(gamma1))
			cbeTriangle(sx + dist1 * Cos(gamma1), sy - dist1 * Sin(gamma1), sx + dist0 * Cos(gamma0), sy - dist0 * Sin(gamma0), sx + 2 * SS * Cos(gamma1), sy - 2 * SS * Sin(gamma1))
		Next c
		cbeSetBlendMode(CBE_BLEND_RESET, 1)
	DrawToScreen
End Function

Function UpdateClouds()
	For c.CloudBlock = Each CloudBlock
		c\x = c\x + c\xp
		c\y = c\y + c\yp
		
		If (c\x < -10-c\r And c\xp < 0) Then Delete c
		if (c\y > SW+10+c\r And c\xp > 0) Then Delete c
	Next c
	If (Rand(2000) = 0) Then
		If (Rand(1)) Then
			nx = -200
		Else
			nx = SW+200
		EndIf
		GenerateCloud(nx, Rand(100, 350))
	EndIf
End Function

Function UpdateEntitets()
	tPlane.Plane = ConvertToType(player)
	
	tPlane\xp = tPlane\speed * Cos(tPlane\angle)
	tPlane\yp = -tPlane\speed * Sin(tPlane\angle)
	tPlane\x = tPlane\x + tPlane\xp
	tPlane\y = tPlane\y + tPlane\yp

	' Tuhotaan pelaaja
	If IsHittingGround(tPlane\x, tPlane\y) Then
		Lose()
	EndIf

	If Rand(100) = 0 Then GenerateEnemy()

	For en.Enemy = Each Enemy
		en\x = en\x + en\xp
		en\y = en\y + en\yp
		
		If (en\x < 0 And en\xp < 0) Or (en\y > SW And en\xp > 0) Then
			Delete en
		EndIf
	Next en

	For b.Bomb = Each Bomb
		b\yp = b\yp + b\m

		b\x = b\x + b\xp
		b\y = b\y + b\yp
		h = IsHittingEnemy(b\x, b\y)
		If IsHittingGround(b\x, b\y) Or h <> -1 Then 
			If ( h <> -1 ) Then
				hen.Enemy = ConvertToType(h)
				tPlane\points = tPlane\points + hen\mhealth * 100
				Delete hen
			EndIf
			GenerateExplode(b\x, b\y, Distance(0, 0, b\xp, b\yp))
			Delete b
		EndIf
	Next b


	For p.Particle = Each Particle
		p\yp = p\yp + p\m

		p\x = p\x + p\xp
		p\y = p\y + p\yp
		
		' Tuhotaan partikkelit
		If IsHittingGround(p\x, p\y) Then 
			Delete p
		EndIf
	Next p

	For e.Explode= Each Explode
		e\radius = CurveValue(e\radius, e\mradius, 10)
		e\alpha = CurveValue(0, e\alpha, 30)
		e\r = CurveValue(e\tr, e\r, 10)
		e\g = CurveValue(e\tg, e\g, 10)
		e\b = CurveValue(e\tb, e\b, 10)
		If (Timer() > e\astart+e\atime) Then Delete e
	Next e
End Function

Function DrawEntitets()
	tPlane.Plane = ConvertToType(player)
	Color 0,0,0
	cbeTriangle(tPlane\x + tPlane\length * Cos(tPlane\angle-90), tPlane\y - tPlane\length * Sin(tPlane\angle-90), tPlane\x + tPlane\length * Cos(tPlane\angle+90), tPlane\y - tPlane\length * Sin(tPlane\angle+90), tPlane\x + tPlane\length * Cos(tPlane\angle), tPlane\y - tPlane\length * Sin(tPlane\angle))
	Color 255,255,255
	cbeTriangle(tPlane\x + tPlane\length * Cos(tPlane\angle-90), tPlane\y - tPlane\length * Sin(tPlane\angle-90), tPlane\x + tPlane\length * Cos(tPlane\angle+90), tPlane\y - tPlane\length * Sin(tPlane\angle+90), tPlane\x + tPlane\length * Cos(tPlane\angle), tPlane\y - tPlane\length * Sin(tPlane\angle), False)

	For en.Enemy = Each Enemy
		Color 0,0,0
		Box en\x-en\sizeW/2, en\y-en\sizeH/2, en\sizeW, en\sizeH
		Color 255,255,255
		Box en\x-en\sizeW/2, en\y-en\sizeH/2, en\sizeW, en\sizeH, False
	Next en

	For b.Bomb = Each Bomb
		Color 255,255,0
		Dot b\x, b\y
	Next b

	For p.Particle = Each Particle
		Color p\r, p\g, p\b
		Dot p\x, p\y
	Next p

	For e.Explode= Each Explode
		cbeColor(e\r, e\g, e\b, e\alpha)
		Circle e\x-e\radius/2, e\y-e\radius/2, e\radius
	Next e
End Function 

Function DrawBG()
	DrawImage BGsky, 0, 0
	DrawImage skyMasked, 0, 0
End Function

Function DrawClouds()
	For c.CloudBlock = Each CloudBlock
		cbeColor(c\r, c\g, c\b, c\a)
		Circle c\x - c\radius, c\y - c\radius, c\radius * 2
	Next c
End Function

Function DrawGround() 

	For i = 0 To SW
		Color 120, 120, 120
		Line i, SH-Ground(i, 0), i, SH
		Select Ground(i, 1)
			Case 0
				Color 200, 200, 200
			Case 1 'GROUND_DIRT
				Color 123, 90, 0
				Line i, SH-Ground(i, 0), i, SH-Ground(i, 0)+Ground(i, 3)

				Color 128, 168, 4
			Case 2 'GROUND_BUILDING
				Color 123, 90, 0
				Line i, SH-(Ground(Ground(i, 2)-1, 0)), i, SH-(Ground(Ground(i, 2)-1, 0))+Ground(i, 3)

				Color 64,64,64
				Line i, SH-Ground(i, 0), i, SH-Ground(i, 0)-Ground(i, 4)
		End Select

'		If (i > 0) Then Line i, SH-Ground(i, 0), i-1, SH-Ground(i-1, 0)
		Dot i-1, SH-Ground(i, 0)

	Next i
End Function 

Function DrawGUI() 
	tPlane.Plane = ConvertToType(player)
	Color 255, 255, 255

    Circle 102,2,20,OFF
    Line 112,12,112+Cos(tPlane\angle) * 10, 12-Sin(tPlane\angle) * 10

	Text 0, 0, tPlane\x + ", " + tPlane\y
	Text 0, 12, "s:"+tPlane\length + ", a" + tPlane\angle + ", s" + tPlane\speed

	Text 10, 30, dbx+":"+Ground(dbx, 0)+"|"+Ground(dbx, 1)+"|"+Ground(dbx, 2)+"|"+Ground(dbx, 3)+"|"+Ground(dbx, 4)

	Line dbx, 350, dbx, 450
	
	Text 10, SH-22, "p:"+tPlane\points
End Function 

Function GenerateCloud(nx, ny)
	For i = 0 To Rand(3,7)
		Print "New Cloud"
		nr = Rand(60,100)
		nxp# = Rnd(0.5, 0.7)
		If (Rand(1)) Then nxp# = -Rnd(0.2, 0.5)
		e = 120
		While e > 40
			Print "New CloudBlock"
			c.CloudBlock = New(CloudBlock)
			c\x = nx + Cos(e) * nr
			c\y = ny - Sin(e) * nr
			c\xp = nxp
			c\radius = Rand(10,20)
			c\r = Rand(200,240)
			c\g = Rand(200,240)
			c\b = Rand(200,240)
			c\a = 255
			e = e - Rand(7,10)
		Wend
	Next i
End Function

Function GenerateEnemy()
	en.Enemy = New(Enemy)
	If (Rand(1) = 0) Then
		en\x = -10
		en\xp = 1
	Else 
		en\x = SW+10
		en\xp = -1
	EndIf
	en\y = 300 + Rand(-20, 20)
	en\angle = GetAngle(0, 0, en\xp, en\yp)
	en\mhealth = Rand(1,10)
	en\health = en\mhealth
	
	en\sizeW = Rand(10,20)
	en\sizeH = Rand(5,10)
End Function 

Function GenerateClouds()
	For c.CloudBlock = Each CloudBlock
		Delete c
	Next c
	For i = 0 To Rand(2,4)
		GenerateCloud(Rand(SW), Rand(100, 350))
	Next i
End Function 

Function GenerateGround()
	Ground(i, 0) = 200
	Ground(i, 1) = GROUND_DIRT
	Ground(i, 2) = 0
	Ground(i, 3) = 20
	For i = 1 To SW
		r = Rand(100)
		Ground(i, 1) = Ground(i-1, 1)

		Ground(i, 2) = Ground(i-1, 2)

		Select Ground(i-1, 1)
			Case 1
				If (r < 4) Then  Ground(i, 1) = GROUND_BUILDING
			Case 2
				If (r < 7 And (i-Ground(i, 2))>Rand(7,13)) Then  Ground(i, 1) = GROUND_DIRT
		End Select

		Select Ground(i, 1)
			Case 1
				Ground(i, 0) = Ground(i-1, 0)+Rnd(2)-1
				Ground(i, 2) = i
			Case 2
				Ground(i, 0) = Ground(i-1, 0)
				If (Ground(i-1, 1) = GROUND_DIRT) Then
					Ground(i, 4) = Rand(20,40)
				Else
					Ground(i, 4) = Ground(i-1, 4)
				End If
		End Select

		Ground(i, 3) = Ground(i-1, 3)+Rnd(2)-1
		While Ground(i, 3) < 10
			Ground(i, 3) = Ground(i-1, 3)+Rnd(2)-1
		Wend
	Next i
End Function

Function Lose()
'	MakeError "HƒVISIT!"
	ClsColor 255, 0, 0
End Function

Function GenerateExplode(x, y, force)
	e.Explode = New(Explode)
	e\x = x + Rand(-2, 2)
	e\y = y + Rand(-2, 2)
	e\radius = 0
	e\mradius = Rand(20,40)

	e\r = 255
	e\g = 255
	e\b = 0
	e\alpha = 255
	
	e\ttime = Rand(1000,1600)
	e\tr = 255
	e\tg = 0
	e\tb = 0

	e\atime = e\ttime + Rand(800)
	e\astart = Timer()

	For i = 0 To Rand(100, 150)
		p.Particle = New(particle)
		p\x = x + Rand(-2, 2)
		p\y = y + Rand(-2, 2)
		p\xp = Rnd(-2, 2)
		p\yp = Rnd(-2, 2)
		p\m = Rnd(0.2, 0.4)
		p\r = 255
		p\g = 255
		p\b = 0
		
		p\ttime = Rand(1000,1600)
		p\tr = 255
		p\tg = 172
		p\tb = 0

		p\xp = force * Cos(p\a)
		p\yp = -force * Sin(p\a)
	Next i
End Function

Function IsHittingGround(x, y)
	Return (x < 0) Or (x >= SW) Or (y > (SH - Ground(x, 0) - Ground(x, 4)))
End Function

Function IsHittingEnemy(x, y)
	For en.enemy = Each Enemy
		If ( x  > (en\x - en\sizeW/2) ) Then
			If ( x  < (en\x + en\sizeW/2) ) Then
				If ( y  > (en\y - en\sizeH/2) ) Then
					If ( y  < (en\y + en\sizeW/2) ) Then
						Return ConvertToInteger(en)
					EndIf
				EndIf
			EndIf
		EndIf
	Next en
	Return -1
End Function
